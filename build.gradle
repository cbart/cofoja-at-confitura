apply plugin: 'java'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

project.ext {
  v = [
    junit: '4.10',
    cofoja: '1.0-r139',
  ]

  cofoja = files("lib/cofoja-${v.cofoja}.jar")
  yeti = files("lib/yeti/*")

  outputContractsDir = files('build/contracts') {
    builtBy('compileContracts')
  }
}

dependencies {
  compile cofoja
  testCompile "junit:junit:${v.junit}", outputContractsDir
}

compileJava {
  options.compilerArgs = ['-proc:none']
}

task compileContracts(type: Compile) {
  source = sourceSets.main.java.srcDirs
  classpath = cofoja
  destinationDir = outputContractsDir as File
  options.compilerArgs = ['-classpath', "lib/cofoja-${v.cofoja}.jar",
      '-processor', 'com.google.java.contract.core.apt.AnnotationProcessor',
      '-proc:only']
}

test {
  jvmArgs "-javaagent:lib/cofoja-${v.cofoja}.jar"
}

task yeti(type: JavaExec, dependsOn: [compileJava, compileContracts]) {
  main = 'yeti.Yeti'
  classpath = yeti + cofoja + outputContractsDir + files(sourceSets.main.output.classesDir)
  args = ['-Java', '-testModules=pl.confitura.contracts.FirstOrderSolver', '-time=3s',
      '-branchCoverage', "-yetiPath=${sourceSets.main.output.classesDir}", '-cofoja']
  jvmArgs "-javaagent:lib/cofoja-${v.cofoja}.jar"
  print(commandLine)
}