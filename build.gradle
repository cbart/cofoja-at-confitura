apply plugin: 'java'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

project.ext {
  v = [
    joda: '2.1',
    junit: '4.10',
    cofoja: '1.0-r139',
    guava: '12.0'
  ]

  outputPath = sourceSets.main.output.classesDir
  cofoja = files("lib/cofoja-${v.cofoja}.jar")
  yeti = files("lib/yeti/*")

  outputContractsDir = files('build/contracts') {
    builtBy('compileContracts')
  }
}

dependencies {
  compile cofoja, "com.google.guava:guava:${v.guava}",
      "joda-time:joda-time:${v.joda}"
  testCompile "junit:junit:${v.junit}", outputContractsDir
}

compileJava {
  options.compilerArgs = ['-proc:none']
}

task compileContracts(type: Compile) {
  source = sourceSets.main.java.srcDirs
  classpath = configurations.compile
  destinationDir = outputContractsDir as File
  options.compilerArgs = ['-processor',
      'com.google.java.contract.core.apt.AnnotationProcessor',
      '-proc:only']
}

test {
  jvmArgs "-javaagent:lib/cofoja-${v.cofoja}.jar"
}

task yeti(type: JavaExec, dependsOn: [compileJava, compileContracts]) {
  main = 'yeti.Yeti'
  classpath = yeti + outputContractsDir +
      files(outputPath) + configurations.compile
  args = ['-java', '-testModules=pl.confitura.contracts.*',
      '-time=15s', "-yetiPath=${outputPath}",
      '-cofoja', '-gui']
  jvmArgs "-javaagent:lib/cofoja-${v.cofoja}.jar"
}